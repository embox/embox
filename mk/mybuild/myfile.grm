"Name"     = 'MyFile'
"Author"   = 'Eldar Abusalimov'
"Version"  = '0.6'
"About"    = 'Mybuild file'

"Case Sensitive" = True

"Start Symbol" = <MyFile>

! -------------------------------------------------
! Rules
! -------------------------------------------------

<MyFile>
        ::= <Package> <Imports> <Entities>

! Package?
<Package>
        ::= 'package' <QualifiedName>
         |

! Import*
<Imports>
        ::= <Import> <Imports>
         |
<Import>
        ::= 'import' <QualifiedNameWithWildcard>

! AnnotatedType*
<Entities>
        ::= <AnnotatedType> <Entities>
         |
<AnnotatedType>
        ::= <Annotations> <Type>
<Type>
        ::= <ModuleType>
         |  <Interface>
         |  <AnnotationType>

! -------------------------------------------------
! Annotation type.
! -------------------------------------------------

<AnnotationType>
        ::= 'annotation' Identifier '{' <AnnotationMembers> '}'
<AnnotationMembers>
        ::= <AnnotatedAnnotationMember> <AnnotationMembers>
         |
<AnnotatedAnnotationMember>
        ::= <Annotations> <Option>


! -------------------------------------------------
! Interfaces and features.
! -------------------------------------------------

! interface Name (extends ...)? { ... }
<Interface>
        ::= 'interface' Identifier <SuperInterfaces> '{' <Features> '}'

! (extends ...)?
<SuperInterfaces>
        ::= 'extends' <ReferenceList>
         |

! AnnotatedInterfaceMember*
<Features>
        ::= <AnnotatedFeature> <Features>
         |
<AnnotatedFeature>
        ::= <Annotations> <Feature>

! feature Name (extends ...)?
<Feature>
        ::= 'feature' Identifier <SuperFeatures>

! (extends ...)?
<SuperFeatures>
        ::= 'extends' <ReferenceList>
         |

! -------------------------------------------------
! Modules.
! -------------------------------------------------

! (abstract)? module Name (extends ...)? { ... }
<ModuleType>
        ::= <ModuleModifiers> 'module' Identifier <SuperModule> '{' <ModuleMembers> '}'

! ModuleModifier*
<ModuleModifiers>
        ::= <ModuleModifier> <ModuleModifiers>
         |
<ModuleModifier>
        ::= 'static'
         |  'abstract'

! (extends ...)?
<SuperModule>
        ::= 'extends' <Reference>
         |

! AnnotatedModuleMember*
<ModuleMembers>
        ::= <AnnotatedModuleMember> <ModuleMembers>
         |
<AnnotatedModuleMember>
        ::= <Annotations> <ModuleMember>
<ModuleMember>
        ::= 'depends'  <ReferenceList>
         |  'provides' <ReferenceList>
         |  'requires' <ReferenceList>
         |  'source'   <FilenameList>
         |  'object'   <FilenameList>
         |  'option'   <Option>

! Item ( , Item )*
<ReferenceList>
        ::= <Reference> ',' <ReferenceList>
         |  <Reference>

! ( string | number | boolean | Type ) Name ( = ...)?
<Option>
        ::= <OptionType> Identifier <OptionDefaultValue>
<OptionType>
        ::= 'string'
         |  'number'
         |  'boolean'
         |  <Reference>
<OptionDefaultValue>
        ::= '=' <Value>
         |

<FilenameList>
        ::= <Filename> ',' <FilenameList>
         |  <Filename>
<Filename>
        ::= StringLiteral

! -------------------------------------------------
! The grammar fragments below are common for
! both my- and config-files. These include:
!   - Charsets and terminal definitions
!   - Rules for annotations
!   - Option values
!   - Lists of key=value pairs
!   - Qualified names and wrapping references
!
! When modifying them please take care to update
! the corresponding parts in another grammar file.
! -------------------------------------------------

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]

{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

{String Chars} = {Printable} + {HT} - ["\]
{String Esc}   = [rnt''"\]
{Non Quote}    = {Printable} + {Whitespace} - [''\]
{Quote Esc}    = [''\]

{Non Slash}    = {Printable} + {Whitespace} - [/]
{Non Asterisk} = {Printable} + {Whitespace} - [*]

{Not New Line} = {Printable} + {Whitespace} - {CR} - {LF}

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Whitespace     = {Whitespace}+
               | '/*' ( {Non Asterisk} | '*' {Non Slash} )*  '*/'
               | '//' {Not New Line}* ( {CR}? {LF} )?

Identifier     = '^'? {ID Head}{ID Tail}*

StringLiteral  = '"' ( {String Chars} | '\' {String Esc} )* '"'
               | '''''' ( ( ''? '' )? ( {Non Quote} | '\' {Quote Esc} ) )* ''''''

NumberLiteral  = {Digit}+ ( '.' {Digit}+ )?
               | '0x' {Hex Digit}+ ( '.' {Hex Digit}+ )?

BooleanLiteral = 'true' | 'false'


! -------------------------------------------------
! @Annotations.
! -------------------------------------------------

<Annotations>
        ::= <Annotation> <Annotations>
         |
<Annotation>
        ::= '@' <Reference> <AnnotationInitializer>
<AnnotationInitializer>
        ::= '(' <ParametersList> ')'
         |  '(' <Value> ')'
         |

! -------------------------------------------------
! Comma-separated list of param=value pairs.
! -------------------------------------------------

<ParametersList>
        ::= <Parameter> ',' <ParametersList>
         |  <Parameter>
<Parameter>
        ::= <SimpleReference> '=' <Value>

<Value>
        ::= StringLiteral
         |  NumberLiteral
         |  BooleanLiteral
         |  <Reference>

! -------------------------------------------------
! Datatypes.
! -------------------------------------------------

<Reference>
        ::= <QualifiedName>
<SimpleReference>
        ::= Identifier

! -------------------------------------------------
! Extended identifiers.
! -------------------------------------------------

<QualifiedName>
        ::= Identifier '.' <QualifiedName>
         |  Identifier

<QualifiedNameWithWildcard>
        ::= <QualifiedName> '.*'
         |  <QualifiedName>

