"Name"     = 'MyFile'
"Author"   = 'Eldar Abusalimov'
"Version"  = '0.4'
"About"    = 'Mybuild file'

"Case Sensitive" = True

"Start Symbol" = <MyFile>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

!{Whitespace}   = {&09 .. &0D} + {Space}

!{Printable}    = {&20 .. &7F}

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]

{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

{String Chars} = {Printable} + {HT} - ["\]
{String Esc}   = [rnt''"\]
{Non Quote}    = {Printable} + {Whitespace} - [''\]
{Quote Esc}    = [''\]

{Non Slash}    = {Printable} + {Whitespace} - [/]
{Non Asterisk} = {Printable} + {Whitespace} - [*]

{Not New Line} = {Printable} + {Whitespace} - {CR} - {LF}

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Whitespace     = {Whitespace}+
               | '/*' ( {Non Asterisk} | '*' {Non Slash} )*  '*/'
               | '//' {Not New Line}* ( {CR}? {LF} )?

Identifier     = '^'? {ID Head}{ID Tail}*

StringLiteral  = '"' ( {String Chars} | '\' {String Esc} )* '"'
               | '''''' ( ( ''? '' )? ( {Non Quote} | '\' {Quote Esc} ) )* ''''''

NumberLiteral  = {Digit}+ ( '.' {Digit}+ )?
               | '0x' {Hex Digit}+ ( '.' {Hex Digit}+ )?

BooleanLiteral = 'true' | 'false'

! -------------------------------------------------
! Rules
! -------------------------------------------------

<MyFile>
        ::= <Package> <Imports> <Entities>

! Package?
<Package>
        ::= 'package' <QualifiedName>
         |

! Import*
<Imports>
        ::= <Import> <Imports>
         |
<Import>
        ::= 'import' <QualifiedNameWithWildcard>

! AnnotatedType*
<Entities>
        ::= <AnnotatedType> <Entities>
         |
<AnnotatedType>
        ::= <AnnotationSpecifiers> <Type>
<Type>
        ::= <Module>
         |  <Interface>
         |  <Annotation>

! -------------------------------------------------
! Annotations.
! -------------------------------------------------

<Annotation>
        ::= 'annotation' Identifier '{' <AnnotationMembers> '}'
<AnnotationMembers>
        ::= <AnnotatedAnnotationMember> <AnnotationMembers>
         |
<AnnotatedAnnotationMember>
        ::= <AnnotationSpecifiers> <Option>

<AnnotationSpecifiers>
        ::= <AnnotationSpecifier> <AnnotationSpecifiers>
         |

<AnnotationSpecifier>
        ::= '@' <Reference> <AnnotationInitializer>
<AnnotationInitializer>
        ::= '(' <AnnotationParametersList> ')'
         |  '(' <Value> ')'
         |
<AnnotationParametersList>
        ::= <AnnotationParameter> ',' <AnnotationParametersList>
         |  <AnnotationParameter>
<AnnotationParameter>
        ::= <SimpleReference> '=' <Value>


! -------------------------------------------------
! Interfaces and features.
! -------------------------------------------------

! interface Name (extends ...)? { ... }
<Interface>
        ::= 'interface' Identifier <SuperInterfaces> '{' <Features> '}'

! (extends ...)?
<SuperInterfaces>
        ::= 'extends' <ReferenceList>
         |

! AnnotatedInterfaceMember*
<Features>
        ::= <AnnotatedFeature> <Features>
         |
<AnnotatedFeature>
        ::= <AnnotationSpecifiers> <Feature>

! feature Name (extends ...)?
<Feature>
        ::= 'feature' Identifier <SuperFeatures>

! (extends ...)?
<SuperFeatures>
        ::= 'extends' <ReferenceList>
         |

! -------------------------------------------------
! Modules.
! -------------------------------------------------

! (abstract)? module Name (extends ...)? { ... }
<Module>
        ::= <ModuleModifiers> 'module' Identifier <SuperModule> '{' <ModuleMembers> '}'

! ModuleModifier*
<ModuleModifiers>
        ::= <ModuleModifier> <ModuleModifiers>
         |
<ModuleModifier>
        ::= 'static'
         |  'abstract'

! (extends ...)?
<SuperModule>
        ::= 'extends' <Reference>
         |

! AnnotatedModuleMember*
<ModuleMembers>
        ::= <AnnotatedModuleMember> <ModuleMembers>
         |
<AnnotatedModuleMember>
        ::= <AnnotationSpecifiers> <ModuleMember>
<ModuleMember>
        ::= 'depends'  <ReferenceList>
         |  'provides' <ReferenceList>
         |  'requires' <ReferenceList>
         |  'source'   <FilenameList>
         |  'object'   <FilenameList>
         |  'option'   <Option>

! ( string | number | boolean ) Name ( = ...)?
<Option>
        ::= <OptionType> Identifier <OptionDefaultValue>
<OptionType>
        ::= 'string'
         |  'number'
         |  'boolean'
<OptionDefaultValue>
        ::= '=' <Value>
         |

<Value>
        ::= StringLiteral
         |  NumberLiteral
         |  BooleanLiteral

! -------------------------------------------------
! Datatypes.
! -------------------------------------------------

<Reference>
        ::= <QualifiedName>
<SimpleReference>
        ::= Identifier
<Filename>
        ::= StringLiteral

! -------------------------------------------------
! Comma separated lists.
! -------------------------------------------------

! Item ( , Item )*
<ReferenceList>
        ::= <Reference> ',' <ReferenceList>
         |  <Reference>
<FilenameList>
        ::= <Filename> ',' <FilenameList>
         |  <Filename>

! -------------------------------------------------
! Extended identifiers.
! -------------------------------------------------

<QualifiedName>
        ::= Identifier '.' <QualifiedName>
         |  Identifier

<QualifiedNameWithWildcard>
        ::= <QualifiedName> '.*'
         |  <QualifiedName>

