"Name"     = 'ConfigFile'
"Author"   = 'Eldar Abusalimov'
"Version"  = '0.1'
"About"    = 'Mybuild configuration file'

"Case Sensitive" = True

"Start Symbol" = <ConfigFile>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

!{Whitespace}   = {&09 .. &0D} + {Space}

!{Printable}    = {&20 .. &7F}

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]

{Hex Digit}    = {Digit} + [abcdef] + [ABCDEF]

{String Chars} = {Printable} + {HT} - ["\]
{String Esc}   = [rnt''"\]
{Non Quote}    = {Printable} + {Whitespace} - [''\]
{Quote Esc}    = [''\]

{Non Slash}    = {Printable} + {Whitespace} - [/]
{Non Asterisk} = {Printable} + {Whitespace} - [*]

{Not New Line} = {Printable} + {Whitespace} - {CR} - {LF}

! -------------------------------------------------
! Terminals
! -------------------------------------------------

Whitespace     = {Whitespace}+
               | '/*' ( {Non Asterisk} | '*' {Non Slash} )*  '*/'
               | '//' {Not New Line}* ( {CR}? {LF} )?

Identifier     = '^'? {ID Head}{ID Tail}*

StringLiteral  = '"' ( {String Chars} | '\' {String Esc} )* '"'
               | '''''' ( ( ''? '' )? ( {Non Quote} | '\' {Quote Esc} ) )* ''''''

NumberLiteral  = {Digit}+ ( '.' {Digit}+ )?
               | '0x' {Hex Digit}+ ( '.' {Hex Digit}+ )?

BooleanLiteral = 'true' | 'false'

! -------------------------------------------------
! Rules
! -------------------------------------------------

<ConfigFile>
        ::= <Package> <Imports> <Configurations>

! Package?
<Package>
        ::= 'package' <QualifiedName>
         |

! Import*
<Imports>
        ::= <Import> <Imports>
         |
<Import>
        ::= 'import' <QualifiedNameWithWildcard>

! Configuration*
<Configurations>
        ::= <Configuration> <Configurations>
         |

! configuration Name { ... }
<Configuration>
        ::= 'configuration' Identifier '{' <ConfigurationMembers> '}'

! ConfigurationMember*
<ConfigurationMembers>
        ::= <ConfigurationMember> <ConfigurationMembers>
         |
<ConfigurationMember>
        ::= <Include>

<Include>
        ::= 'include' <ReferenceWithInitializerList>

<ReferenceWithInitializerList>
        ::= <ReferenceWithInitializer> ',' <ReferenceWithInitializerList>
         |  <ReferenceWithInitializer>

<ReferenceWithInitializer>
        ::= <Reference> <Initializer>

<Initializer>
        ::= '(' <ParametersList> ')'
         |
<ParametersList>
        ::= <Parameter> ',' <ParametersList>
         |  <Parameter>
<Parameter>
        ::= <SimpleReference> '=' <Value>

<Value>
        ::= StringLiteral
         |  NumberLiteral
         |  BooleanLiteral

! -------------------------------------------------
! Datatypes.

<Reference>
        ::= <QualifiedName>
<SimpleReference>
        ::= Identifier

! -------------------------------------------------
! Extended identifiers.

<QualifiedName>
        ::= Identifier '.' <QualifiedName>
         |  Identifier

<QualifiedNameWithWildcard>
        ::= <QualifiedName> '.*'
         |  <QualifiedName>

